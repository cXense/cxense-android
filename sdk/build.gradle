apply plugin: 'com.android.library'

ext {
    sdkPublicName = 'Unified'
    authority = 'CxSdkInitProvider'
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.0'


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "$version"
        buildConfigField "String", "SDK_NAME", '"cxense"'
        buildConfigField "String", "SDK_ENDPOINT", '"https://api.cxense.com"'
        buildConfigField "String", "AUTHORITY", "APPLICATION_ID + \".$authority\" "
        buildConfigField "String", "CX_USER", "\"${project.getProperties().get("CX_USER")}\""
        buildConfigField "String", "CX_KEY", "\"${project.getProperties().get("CX_KEY")}\""
        buildConfigField "String", "CX_SITE_ID", "\"${project.getProperties().get("CX_SITE_ID")}\""

        manifestPlaceholders = [authority: "$authority"]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        // Don't abort if Lint finds an error, otherwise the Jenkins build
        // will be marked as failed, and Jenkins won't analyse the Lint output
        abortOnError false
    }
}

dependencies {
    api "com.android.support:support-annotations:$appCompatVersion"
    implementation "com.google.android.gms:play-services-ads:$playServicesVersion"
    api "com.squareup.retrofit2:retrofit:$retrofitVersion"
    api "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    api "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
}

apply from: "$rootDir/gradle/javaDoc.gradle"
apply from: "$rootDir/gradle/upload.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/code-quality.gradle"
